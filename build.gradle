// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.android.application' version '8.5.1' apply false
    id 'com.google.gms.google-services' version '4.4.2' apply false
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin' version '2.0.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.0' apply false
}

subprojects {
    afterEvaluate {
        if (plugins.hasPlugin("com.android.library") ||
                plugins.hasPlugin("com.android.application")) {
            android {
                compileSdkVersion 34
                buildToolsVersion "34.0.0"
                ndkVersion "26.3.11579264"

                lint {
                    checkReleaseBuilds false
                    disable 'MissingTranslation', 'ExtraTranslation', 'BlockedPrivateApi'
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17

                    coreLibraryDesugaringEnabled true
                }

                defaultConfig {
                    minSdkVersion 21
                    //noinspection OldTargetApi
                    targetSdkVersion 33
                }

                buildTypes {
                    play {
                        initWith release
                    }
                }

                buildFeatures.buildConfig = true
            }

            dependencies {
                coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs_nio:2.0.4'
            }
        }
    }
}

tasks.register('clean', Delete) {
    delete layout.buildDirectory.get()
}
