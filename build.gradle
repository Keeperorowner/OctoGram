// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.7.1' apply false
    id 'com.google.gms.google-services' version '4.4.2' apply false
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin' version '2.0.1' apply false
    id 'org.jetbrains.kotlin.android' version '2.0.21' apply false
}

subprojects {
    afterEvaluate {
        if (plugins.hasPlugin("com.android.library") ||
                plugins.hasPlugin("com.android.application")) {
            android {
                compileSdkVersion 35
                buildToolsVersion "35.0.0"
                ndkVersion "27.2.12479018"

                lint {
                    checkReleaseBuilds false
                    disable 'MissingTranslation', 'ExtraTranslation', 'BlockedPrivateApi'
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_21
                    targetCompatibility JavaVersion.VERSION_21

                    coreLibraryDesugaringEnabled true
                }

                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 34
                }

                buildTypes {
                    play {
                        initWith release
                    }
                }

                buildFeatures.buildConfig = true
            }

            dependencies {
                //noinspection GradleDependency
                coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs_nio:2.0.4'
            }
        }
    }
}

tasks.register('clean', Delete) {
    delete layout.buildDirectory.get()
}
